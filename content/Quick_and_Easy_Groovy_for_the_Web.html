title=Quick and Easy Groovy for the Web
date=2011-06-22 12:35:56.924
type=post
status=published
tags=Work, Java, Computers
~~~~~~
<p><a href = "http://groovy-lang.org/">Groovy</a> can be used pretty easily to spin up some simple web pages in almost the same way one would hack out some PHP or JSP without going to the trouble to do an all-out <a href = "http://grails.org/">Grails</a> project.</p>
<p>The <a href = "http://groovy-lang.org/servlet.html">Groovy Servlet</a> allows you to pack up the <code>groovy-all-*.jar</code>, a simple <code>web.xml</code>, and whatever <code>*.groovy</code> scripts you want and deploy it right into Tomcat as a plain WAR file.  The Groovy Servlet page </p>
<p>Here's a bit of a script I put together to jump start a simple Groovlet project by packaging a WAR file from a directory of scripts.  This isn't Groovy Servlet code itself, but just a command-line tool.  (The Groovy Servlet page linked previously has examples for writing your own servlets.) This script will copy in the Groovy JAR and generate the basic <code>web.xml</code> to wire up the <code>GroovyServlet</code> to dynamically execute your scripts.  I also have a downloadable copy of <a href = "http://www.hjsoft.com/~john/package_groovlet.groovy">package_groovlet.groovy</a>.</p>
<blockquote>
<pre>#!/usr/bin/env groovy

if (args.size() &lt; 1) {
    print """\
        |Usage: package_groovlet.groovy &lt;war-name&gt;
        |Package the current directory into a Groovy Servlet war.
        |""".stripMargin()
    return
}

def war = args[0]
def embed = "${System.getenv()['GROOVY_HOME']}/embeddable"

def ant = new AntBuilder()

ant.sequential {
    delete(dir: 'build')
    mkdir(dir: 'build/WEB-INF/lib')
    copy(toDir: 'build/WEB-INF/lib') {
        fileset(dir: embed) {
            include(name: 'groovy-all-*.jar')
        }
    }
    copy(toDir: 'build') {
        fileset(dir: '.') {
            exclude(name: 'build/**')
        }
    }
}

new FileOutputStream('build/WEB-INF/web.xml').withWriter { webxml -&gt;
    webxml.print """\
        &lt;!DOCTYPE web-app PUBLIC
          "-//Sun Microsystems, Inc.//DTD Web Application 2.3//EN"
          "http://java.sun.com/dtd/web-app_2_3.dtd" &gt;
        &lt;web-app&gt;
            &lt;servlet&gt;
                &lt;servlet-name&gt;Groovy&lt;/servlet-name&gt;
                &lt;servlet-class&gt;groovy.servlet.GroovyServlet&lt;/servlet-class&gt;
            &lt;/servlet&gt;
            &lt;servlet-mapping&gt;
                &lt;servlet-name&gt;Groovy&lt;/servlet-name&gt;
                &lt;url-pattern&gt;*.groovy&lt;/url-pattern&gt;
            &lt;/servlet-mapping&gt;
         &lt;/web-app&gt;
     """.stripIndent()
}

ant.jar(destfile: "build/${war}", basedir: 'build')
println "Created build/${war}"</pre>
</blockquote>
